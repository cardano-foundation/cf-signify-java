apply plugin: 'maven-publish'
apply plugin: 'signing'

if (!name.equalsIgnoreCase("components")) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar
                artifactId 'signify'

                pom {
                    name = 'signify'
                    description = 'signify is a Java library for verifying signed messages.'
                    url = 'https://github.com/cardanofoundation/cf-signify-java'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
//                            id = ''
//                            name = ''
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/cardanofoundation/cf-signify-java'
                        developerConnection = 'scm:git:ssh://git@github.com/cardanofoundation/cf-signify-java'
                        url = 'https://github.com/cardanofoundation/cf-signify-java'
                    }
                }
            }
        }

        repositories {
            String ossrhUsername = System.getenv('MAVEN_USERNAME')
            String ossrhPassword = System.getenv('MAVEN_PASSWORD')

            maven {
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    // configure your javadoc options here, e.g.:
    options.addStringOption('Xdoclint:none', '-quiet')
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

if (isReleaseVersion && !project.hasProperty("skipSigning")) {
    signing {
        useGpgCmd()
        sign publishing.publications
    }
}
