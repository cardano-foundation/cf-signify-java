apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jreleaser'

if (!name.equalsIgnoreCase("components")) {
    java {
        withJavadocJar()
        withSourcesJar()
    }
    javadoc {
        options {
            failOnError false
        }
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifactId 'signify'

                pom {
                    name = 'signify'
                    description = 'Signify is an edge client for the KERI and ACDC identity protocols.'
                    url = 'https://github.com/cardano-foundation/cf-signify-java'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                           id = 'CF'
                           name = 'Cardano Foundation'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/cardano-foundation/cf-signify-java'
                        developerConnection = 'scm:git:ssh://git@github.com/cardano-foundation/cf-signify-java'
                        url = 'https://github.com/cardano-foundation/cf-signify-java'
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'build'
                url = rootProject.projectDir.path + '/build/staging-deploy'
            }

            if (version.endsWith('SNAPSHOT')) {
                String mavenCentralUsername = System.getenv('JRELEASER_MAVENCENTRAL_USERNAME')
                String mavenCentralPassword = System.getenv('JRELEASER_MAVENCENTRAL_PASSWORD')
                maven {
                    name = 'mavenCentralSnapshot'
                    url = 'https://central.sonatype.com/repository/maven-snapshots/'
                    credentials {
                        username mavenCentralUsername
                        password mavenCentralPassword
                    }
                }
            }
        }
    }


    jreleaser {
        signing {
            active = "ALWAYS"
            mode = "COMMAND"
        }
        deploy {
            maven {
                mavenCentral {
                    create("release-deploy") {
                        active = "ALWAYS"
                        url = "https://central.sonatype.com/api/v1/publisher"
                        applyMavenCentralRules = true
                        stagingRepository("build/staging-deploy")
                    }
                }
            }
        }
    }

}
