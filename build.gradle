plugins {
    id 'maven-publish'
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
    id 'org.ajoberstar.grgit' version '5.2.0'
    id 'org.jreleaser' version '1.19.0'
    id("org.openapi.generator") version "7.16.0"
}

repositories {
    mavenCentral()
    maven {
        url 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

group = 'org.cardanofoundation'
version = '0.1.2-SNAPSHOT'

def commit_id = getCheckedOutGitCommitHash()
if (project.version.endsWith("-SNAPSHOT")) {
    version = "${project.version}".replace("-SNAPSHOT", "-${commit_id}-SNAPSHOT")
}

def getCheckedOutGitCommitHash() {
    grgit.head().abbreviatedId
}

apply from: './publish-common.gradle'

// Java toolchain configuration - Use Java 21 for modern features
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def lombokVersion = '1.18.30'
def lazysodiumVersion = '5.1.4'
def jnaVersion = '5.15.0'
def junitVersion = '5.10.0'
def math3Version = '3.6.1'
def jacksonVersion = '2.18.1'
def bouncycastleVersion = '1.79'
def jsonVersion = '20240303'
def structuredFieldsVersion = '0.4'
def jakartaAnnotationVersion = '2.1.1'
def jacksonNullableVersion = '0.2.6'
def jakartaValidationVersion = '3.0.2'
def hibernateValidatorVersion = '8.0.1.Final'

// test dependencies
def testngVersion = '7.7.0'
def mockitoVersion = '5.5.0'
def mockwebVersion = '4.12.0'

dependencies {
    implementation "com.goterl:lazysodium-java:${lazysodiumVersion}"
    implementation "net.java.dev.jna:jna:${jnaVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.apache.commons:commons-math3:${math3Version}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    // TODO: consider a smaller blake-only package, so long as it's well used and safe of vulnerabilities.
    implementation "org.bouncycastle:bcprov-jdk18on:${bouncycastleVersion}"
    implementation "org.json:json:${jsonVersion}"
    implementation "org.greenbytes.http:structured-fields:${structuredFieldsVersion}"
    implementation "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationVersion}"
    implementation "org.openapitools:jackson-databind-nullable:${jacksonNullableVersion}"

    // Jakarta Validation for generated models (required when useBeanValidation=true)
    implementation "jakarta.validation:jakarta.validation-api:${jakartaValidationVersion}"
    implementation "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"

    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${mockwebVersion}"
    testImplementation("org.testng:testng:${testngVersion}")
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}

allure {
    version = '2.13.6'
}

// JUnit Tests (unit tests)
tasks.test {
    useJUnitPlatform()  // Use JUnit 5 platform
    filter {
        includeTestsMatching "org.cardanofoundation.signify.app.*"
        includeTestsMatching "org.cardanofoundation.signify.cesr.*"
        includeTestsMatching "org.cardanofoundation.signify.core.*"
        includeTestsMatching "org.cardanofoundation.signify.end.*"
    }
    finalizedBy 'allureReport'
}

// Separate task for TestNG tests (E2E tests)
tasks.register('testE2E', Test) {
    maxParallelForks = 1
    def startTime = [:]
    def totalTests = 0
    def passedTests = 0
    def failedTests = 0
    def skippedTests = 0
    def suiteStartTime

    useJUnitPlatform()
    doFirst {
        suiteStartTime = System.nanoTime()
    }
    filter {
        includeTestsMatching "org.cardanofoundation.signify.e2e.*"
    }

    beforeTest { descriptor ->
        startTime[descriptor] = System.currentTimeSeconds()
        def now = java.time.LocalDateTime.now()
        def formatter = java.time.format.DateTimeFormatter.ofPattern("dd-MM-yyyy HH:mm:ss")
        def formattedDateTime = now.format(formatter)
        println "\n=================================================================================\n"
        println "Start time: ${formattedDateTime}"
        println "Starting test: ${descriptor.className}"
    }
    afterTest { descriptor, result ->
        totalTests++
        if (result.resultType == TestResult.ResultType.SUCCESS) {
            passedTests++
        } else if (result.resultType == TestResult.ResultType.FAILURE) {
            failedTests++
        } else if (result.resultType == TestResult.ResultType.SKIPPED) {
            skippedTests++
        }
        def duration = System.currentTimeSeconds() - startTime[descriptor]
        println "Finished test: ${descriptor.className} in ${duration} seconds. Waiting for 2 seconds..."
        Thread.sleep(2000)

    }

    afterSuite { desc, result ->
        if (desc.parent == null) { // Kiểm tra nếu là toàn bộ test suite
            def totalDuration  = (System.nanoTime() - suiteStartTime) / 1_000_000_000
            println "\n================================================================================="
            println "Test Summary:"
            println "Total Tests: ${totalTests}"
            println "Passed: ${passedTests}"
            println "Failed: ${failedTests}"
            println "Skipped: ${skippedTests}"
            println "Total Execution Time: ${totalDuration} seconds"
            println "=================================================================================\n"
        }
    }
    finalizedBy 'allureReport'
}


tasks.register("cleanAllure") {
    group = "cleanup"
    description = "Clean Allure results and reports"
    doLast {
        delete(file("build/allure-results"))
        delete(file("build/allure-report"))
    }
}

tasks.named("clean") {
    dependsOn("cleanAllure")
}


// Custom Allure report task for unit tests
tasks.register('allureUnitTestReport') {
    doLast {
        copy {
            from "$buildDir/allure-results" // Default results dir
            into "$buildDir/allure-results/unitTest"
        }
        javaexec {
            main = 'io.qameta.allure.cli.AllureCommand'
            classpath = configurations.allure
            args = ['generate', "$buildDir/allure-results/unitTest", '-o', "$buildDir/reports/allure-report/unitTest"]
        }
    }
}

// Custom Allure report task for E2E tests
tasks.register('allureE2ETestReport') {
    doLast {
        copy {
            from "$buildDir/allure-results" // Default results dir
            into "$buildDir/allure-results/e2eTest"
        }
        javaexec {
            main = 'io.qameta.allure.cli.AllureCommand'
            classpath = configurations.allure
            args = ['generate', "$buildDir/allure-results/e2eTest", '-o', "$buildDir/reports/allure-report/e2eTest"]
        }
    }
}

// Disable the default allureReport task if custom tasks are used
tasks.named('allureReport') {
    enabled = false
}

tasks.openApiGenerate {
    generatorName.set("java")
    def generatedSpec = layout.buildDirectory.file("generated/spec/keria-openapi.yaml")
    inputSpec.set(generatedSpec.map { it.asFile.absolutePath })
    outputDir.set("$rootDir".toString())
    validateSpec.set(false)
    skipValidateSpec.set(true)
    modelPackage.set("org.cardanofoundation.signify.generated.keria.model")
    invokerPackage.set("org.cardanofoundation.signify.generated.keria.model")

    globalProperties.set([
        models        : "",   // generate models only
        modelDocs     : "false",
        modelTests    : "false",
    ])

    configOptions = [
            library              : "resttemplate",
            serializationLibrary : "jackson",
            useJakartaEe         : "true",
            dateLibrary          : "java21",
            sourceFolder         : "src/main/java",

            // ====== oneOf Handling ======
            // Generate interfaces for oneOf schemas - creates TypeScript-like union types
            useOneOfInterfaces   : "true",      // Generates interfaces instead of flat classes (TypeScript-like)
            legacyDiscriminatorBehavior: "false",

            // ====== Reduce Boilerplate ======
            hideGenerationTimestamp: "true",
            openApiNullable      : "false",     // Don't use JsonNullable wrapper

            // ====== Code Quality & Validation ======
            generateConstructorWithAllArgs: "true",
            performBeanValidation: "true",      // Requires jakarta.validation dependencies
            useBeanValidation    : "true",      // Requires jakarta.validation dependencies
    ]

//    nameMappings.put("A", "AUpper")
}

tasks.register("prepareOpenApiSpec") {
    group = "openapi"
    description = "Fetches keria OpenAPI spec from SPEC_URL"

    def generatedSpec = layout.buildDirectory.file("generated/spec/keria-openapi.yaml")
    outputs.file(generatedSpec)

    doLast {
        def specUrl = System.getenv("SPEC_URL") ?: "http://localhost:3902/spec.yaml"
        def target = generatedSpec.get().asFile
        target.parentFile.mkdirs()

        logger.lifecycle("Downloading OpenAPI spec from ${specUrl}")
        try {
            target.withOutputStream { out ->
                new URL(specUrl).withInputStream { input -> out << input }
            }
        } catch (Exception ex) {
            throw new GradleException("Failed to download spec from ${specUrl}. Set SPEC_URL to a reachable spec endpoint.", ex)
        }
        logger.lifecycle("Spec available at ${target}")
    }
}

tasks.named("openApiGenerate") {
    dependsOn("prepareOpenApiSpec")
}