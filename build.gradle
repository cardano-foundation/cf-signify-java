plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group = 'org.cardanofoundation'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def lombokVersion = '1.18.30'
def lazysodiumVersion = '5.1.4'
def jnaVersion = '5.15.0'
def junitVersion = '5.10.0'
def math3Version = '3.6.1'
def jacksonVersion = '2.18.1'
def mockwebVersion = '4.12.0'
def slf4jVersion = '2.0.7'
def bouncycastleVersion = '1.79'
def testngVersion = '7.7.0'
def jsonVersion = '20240303'
def mockitoVersion = '5.5.0'

dependencies {
    implementation "com.goterl:lazysodium-java:${lazysodiumVersion}"
    implementation "net.java.dev.jna:jna:${jnaVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.apache.commons:commons-math3:${math3Version}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    // TODO: consider a smaller blake-only package, so long as it's well used and safe of vulnerabilities.
    implementation "org.bouncycastle:bcprov-jdk18on:${bouncycastleVersion}"
    implementation "org.json:json:${jsonVersion}"

    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "com.squareup.okhttp3:mockwebserver:${mockwebVersion}"
    testImplementation("org.testng:testng:${testngVersion}")
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}

allure {
    version = '2.13.6'
}

// JUnit Tests (unit tests)
tasks.test {
    useJUnitPlatform()  // Use JUnit 5 platform
    finalizedBy 'allureReport'
}

// Separate task for TestNG tests (E2E tests)
tasks.register('testNG', Test) {
    useTestNG()  // Use TestNG for E2E tests
    testLogging {
        events "passed", "failed", "skipped"
    }
    finalizedBy 'allureReport'
}


// Custom Allure report task for unit tests
tasks.register('allureUnitTestReport') {
    doLast {
        copy {
            from "$buildDir/allure-results" // Default results dir
            into "$buildDir/allure-results/unitTest"
        }
        javaexec {
            main = 'io.qameta.allure.cli.AllureCommand'
            classpath = configurations.allure
            args = ['generate', "$buildDir/allure-results/unitTest", '-o', "$buildDir/reports/allure-report/unitTest"]
        }
    }
}

// Custom Allure report task for E2E tests
tasks.register('allureE2ETestReport') {
    doLast {
        copy {
            from "$buildDir/allure-results" // Default results dir
            into "$buildDir/allure-results/e2eTest"
        }
        javaexec {
            main = 'io.qameta.allure.cli.AllureCommand'
            classpath = configurations.allure
            args = ['generate', "$buildDir/allure-results/e2eTest", '-o', "$buildDir/reports/allure-report/e2eTest"]
        }
    }
}